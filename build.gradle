plugins {
    id 'java'
    id 'jacoco'
    id 'pmd'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '7.2.1'
    id 'org.openapi.generator' version '7.14.0'
}

group = 'com.github.ismail2ov'
version = '0.0.1-SNAPSHOT'
description = 'An example of effective mutation testing in Spring Boot 3 projects with Gradle'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot starters
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Database & migrations
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation 'io.hypersistence:hypersistence-utils-hibernate-63:3.10.3'
    runtimeOnly 'org.postgresql:postgresql'

    // OpenAPI / Swagger
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.36'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.11'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // MapStruct
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    // Test utils
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'com.tngtech.archunit:archunit:1.4.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy(tasks.jacocoTestReport)
}

spotless {
    java {
        eclipse().configFile(file('configuration/CodeStyle.xml'))
        removeUnusedImports()
        importOrder('java', 'jakarta', 'org.springframework', 'org', 'com', 'com.github.ismail2ov')
        target 'src/**/*.java'
    }
}

openApiGenerate {
    generatorName = 'spring'
    inputSpec = file('src/main/resources/openapi/definition.yml').path
    outputDir = layout.buildDirectory.dir('generated').get().asFile.path
    apiPackage = 'com.github.ismail2ov.ecommerce.infrastructure.controller.api'
    modelPackage = 'com.github.ismail2ov.ecommerce.infrastructure.controller.model'
    modelNameSuffix = 'RDTO'
    configOptions = [
            useSpringBoot3      : 'true',
            useJakartaEe        : 'true',
            interfaceOnly       : 'true',
            skipDefaultInterface: 'true'
    ]
}

sourceSets.main.java.srcDirs += layout.buildDirectory.dir('generated/src/main/java').get().asFile

tasks.named('compileJava') {
    dependsOn tasks.named('spotlessApply'), tasks.named('openApiGenerate')
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += [
            '-Amapstruct.suppressGeneratorTimestamp=true',
            '-Amapstruct.suppressGeneratorVersionInfoComment=true',
            '-Amapstruct.defaultComponentModel=spring',
            '-Amapstruct.defaultInjectionStrategy=constructor'
    ]
}

jacoco {
    toolVersion = '0.8.13'
}

tasks.jacocoTestReport {
    dependsOn(tasks.test)

    reports {
        xml.required.set(true)
        csv.required.set(false)
        html.outputLocation.set(layout.buildDirectory.dir('reports/jacoco/html'))

        afterEvaluate {
            classDirectories.setFrom(
                    files(classDirectories.files.collect {
                        fileTree(dir: it, exclude: [
                                '**/*Application*',
                                '**/*RDTO*',
                                '**/*MapperImpl*',
                                '**/controller/api/ApiUtil*'
                        ])
                    })
            )
        }
    }
}

tasks.jacocoTestCoverageVerification {
    classDirectories.setFrom(tasks.jacocoTestReport.classDirectories)

    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
}

tasks.check {
    dependsOn(tasks.jacocoTestCoverageVerification)
}

pmd {
    consoleOutput = true
    toolVersion = '7.12.0'
    rulesMinimumPriority = 5
    ruleSetFiles = files('configuration/pmd-rules.xml')
}
